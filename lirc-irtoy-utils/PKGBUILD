# $Id$
# Maintainer: Evan Purkhiser <evanpurkhiser@gmail.com>

pkgbase=lirc
pkgname=('lirc-irtoy-utils')
pkgdesc="Linux Infrared Remote Control utils, with support for the USB IR Toy v2"
pkgver=0.9.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
replaces=('lirc-utils')
provides=('lirc-utils')
conflicts=('lirc-utils')
license=('GPL')
makedepends=('help2man' 'alsa-lib' 'libx11' 'libftdi' 'libirman')
optdepends=('python2: pronto2lirc utility')
depends=('alsa-lib' 'libx11' 'libftdi' 'libirman')
options=('!strip' '!libtool')
install=lirc-utils.install
source=(http://prdownloads.sourceforge.net/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2
        lircd-handle-large-config.patch
        lirc.logrotate
        lirc.service lircm.service irexec.service
        lirc-usb_irtoy.patch
        lirc.tmpfiles)

build() {
  cd "${srcdir}/lirc-${pkgver}"

  patch -Np1 -i "${srcdir}/lirc-usb_irtoy.patch"
  patch -Np1 -i "${srcdir}/lircd-handle-large-config.patch"

  sed -i '/AC_PATH_XTRA/d' configure.ac
  sed -e 's/@X_CFLAGS@//g' \
      -e 's/@X_LIBS@//g' \
      -e 's/@X_PRE_LIBS@//g' \
      -e 's/@X_EXTRA_LIBS@//g' -i Makefile.am tools/Makefile.am
  # fix for new automake #33497
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
  libtoolize
  autoreconf

  PYTHON=python2 ./configure --enable-sandboxed --prefix=/usr \
      --with-driver=usb_irtoy --with-transmitter --with-tty=/dev/ttyACM0 \
      --with-port=none --with-irq=none

  make
}

package() {
  cd "${srcdir}/lirc-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}"/lirc.service "${pkgdir}"/usr/lib/systemd/system/lirc.service
  install -D -m644 "${srcdir}"/lircm.service "${pkgdir}"/usr/lib/systemd/system/lircm.service
  install -D -m644 "${srcdir}"/irexec.service "${pkgdir}"/usr/lib/systemd/system/irexec.service
  install -D -m644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf
  cp -rp remotes "${pkgdir}/usr/share/lirc"
  chmod -R go-w "${pkgdir}/usr/share/lirc/"

  # install the logrotate config
  install -Dm644 "${srcdir}/lirc.logrotate" "${pkgdir}/etc/logrotate.d/lirc"

  install -d "${pkgdir}/etc/lirc"

}
md5sums=('b232aef26f23fe33ea8305d276637086'
         'b70cc9640505205446ec47b7d4779f38'
         '3deb02604b37811d41816e9b4385fcc3'
         'dab8a73bcc5fd5479d8750493d8d97dc'
         'c2e20fe68b034df752dba2773db16ebe'
         '07131d117fcfe9dcd50c453c3a5e9531'
         '70e94ac44053b46c6dd8af0ac27bfb1f'
         'febf25c154a7d36f01159e84f26c2d9a')
